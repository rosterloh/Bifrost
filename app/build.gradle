apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.rosterloh.bifrost"
        version "${version_code}"
        versionName "${version_name}"
        minSdkVersion 21
        targetSdkVersion 23

        buildConfigField("boolean", "ENABLE_DEBUG_TOOLS", "false")
        buildConfigField("String", "GCM_API_KEY", "\"${gcm_api_key}\"")
        buildConfigField("String", "GCM_SENDER_ID", "\"${gcm_sender_id}\"")
        buildConfigField("String", "GENERIC_API_KEY", "\"${oauth2_creds_api_key}\"")
    }

    signingConfigs {
        debug {
            storeFile file("../app/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
            buildConfigField("boolean", "ENABLE_DEBUG_TOOLS", "true")
            buildConfigField("String", "GCM_SERVER_URL", "\"${gcm_server_endpoint}\"")
            resValue("string", "website_host_name", "${website_host_name}")
        }
        release {
            debuggable false
            minifyEnabled true
            // No signing config as we do this separately.
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-project.txt')
            buildConfigField("String", "GCM_SERVER_URL", "\"${gcm_server_endpoint}\"")
            resValue("string", "website_host_name", "${website_host_name}")
        }
    }

    lintOptions {
        abortOnError false
    }

    aaptOptions {
        noCompress 'apk'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    // Include libraries checked into the libs directory.
    compile fileTree(include: ['*.jar'], dir: 'libs')

    // Google Play services split client libs. @see https://developers.google.com/android/guides/setup
    compile "com.google.android.gms:play-services-analytics:${google_play_services_client_library_version}"
    compile "com.google.android.gms:play-services-location:${google_play_services_client_library_version}"
    compile "com.google.android.gms:play-services-maps:${google_play_services_client_library_version}"


    // Android support libraries.
    compile "com.android.support:appcompat-v7:${android_support_lib_version}"
    compile "com.android.support:cardview-v7:${android_support_lib_version}"
    compile "com.android.support:design:${android_support_lib_version}"
    compile "com.android.support:support-v13:${android_support_lib_version}"

    compile 'com.mcxiaoke.volley:library:1.0.19'
}
